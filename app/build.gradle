apply plugin: 'com.android.application'

Properties properties = new Properties()
properties.load(new FileInputStream("./properties/key.properties"))

def getCommitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    lintOptions {
        abortOnError false
        warning 'InvalidPackage' // https://github.com/square/okio/issues/58
    }

    defaultConfig {
        applicationId "com.blstream.as"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "COMMIT_SHA", "\"${getCommitSha()}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {

        release {
            storeFile file(properties.RELEASE_STORE_PATH)
            storePassword properties.RELEASE_STORE_PASSWORD
            keyAlias properties.RELEASE_KEY_ALIAS
            keyPassword properties.RELEASE_KEY_PASSWORD
        }

        debug {
            storeFile file("debug.keystore")
        }
    }


    productFlavors {
        dev {
            buildTypes.debug
            signingConfig signingConfigs.debug

            lintOptions {
                disable 'MissingTranslation'
            }
            versionName "Commit-${getCommitSha()}"
        }
        qa {
            buildTypes.release
            signingConfig signingConfigs.debug
            proguardFile 'proguard-release-rules.pro'
            versionName "Commit-${getCommitSha()}"
        }
        gp {
            buildTypes.release
            signingConfig signingConfigs.release
            proguardFile 'proguard-release-rules.pro'

            lintOptions {
                quiet true
            }
            versionName "Commit-${getCommitSha()}"
        }
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.google.maps.android:android-maps-utils:0.3+'
    compile 'com.twotoasters.clusterkraf:library:1.0.+'
    compile 'com.google.android.gms:play-services:7.0.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    compile 'com.sothree.slidinguppanel:library:3.0.0'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'ch.acra:acra:4.6.2'
    compile project(':data')
    compile project(':ar')
    compile project(':map')
}
